----------------------------
# terraform/environments/prod/wl-configs/afftech.auto.tfvars

```
# ============================================================================
# AGENT WHITE LABEL CONFIGURATION
# ============================================================================
# Agent WLs have Admin panel and Agent panel only.
# NO click functionality exists on Agent WLs.
#
# Template usage:
#   1. Copy this file: cp templates/agent-wl.auto.tfvars.template newwl.auto.tfvars
#   2. Fill in values from Confluence
#   3. Remove all comments and template instructions
#   4. Deploy: terraform apply -var-file="wl-configs/newwl.auto.tfvars"
#
# What will be created:
#   - admin.{domain}     → Admin panel
#   - agent.{domain}     → Agent panel
#   - cdn.{domain}       → CDN distribution
#   - reports.{domain}   → Reports
#
# GitLab CI/CD variables created:
#   - {PLATFORM_CODE}_PROD_BUCKET_NAME
#   - {PLATFORM_CODE}_AGENT_PROD_BUCKET_NAME
# ============================================================================

# ----------------------------------------------------------------------------
# REQUIRED FIELDS
# ----------------------------------------------------------------------------

domain = "afftech.xyz"              # Main domain from Confluence
wl_type = "click"                   # Always "agent" for Agent WLs

platform_code = "AFFTECH"              # Platform code from Confluence (uppercase)

cloudflare_zone_id = "84787ea66aa226406e7c736892c6d493" # Cloudflare Zone ID for main domain

admin_subdomain = "admin"           # Admin subdomain (usually "admin")
# agent_subdomain = "agent"           # Agent subdomain (usually "agent")

gitlab_project_id =  "marketingtech/wl-automation"           # GitLab project ID for CI/CD integration


# click_domain  = "brandx.com"    # Separate click/tracking domain
# click_zone_id = "e1a360e1d6c0ba3423d663c66fc8bacf"     # Cloudflare Zone ID for click domain


# ----------------------------------------------------------------------------
# OPTIONAL FIELDS
# ----------------------------------------------------------------------------

# Custom Subject Alternative Names (SANs) for SSL certificate
# Uncomment if you need additional domains in the certificate
#
# sans = ["www.example.com", "another.example.com"]```
----------------------------

----------------------------
# terraform/environments/prod/wl-configs/owinbet.auto.tfvars

```
# terraform/environments/prod/wl-configs/owinbet.auto.tfvars

domain       = "owinbet.com"
wl_type      = "mixed"
click_domain = "vbnmmmbv.com"```
----------------------------

----------------------------
# terraform/environments/prod/wl-configs/brandx.auto.tfvars

```
# terraform/environments/prod/wl-configs/brandx.auto.tfvars

domain  = "brandx.com"
wl_type = "click"

# Optional
# sans = [
#   "*.brandx.com",
#   "www.brandx.com",
#   "api.brandx.com"
# ]
```
----------------------------

----------------------------
# terraform/environments/prod/outputs.tf

```
# terraform/environments/prod/outputs.tf

output "main_domain_summary" {
  description = "Main domain summary"
  value = length(module.main_domain) > 0 ? {
    domain                     = var.domain
    zone_id                    = module.main_domain[0].zone_id
    regional_certificate_arn   = module.main_domain[0].regional_certificate_arn
    cloudfront_certificate_arn = module.main_domain[0].cloudfront_certificate_arn
    admin                      = module.main_domain[0].admin
    agent                      = module.main_domain[0].agent
    cdn                        = module.main_domain[0].cdn
    reports                    = module.main_domain[0].reports
fix-api-dns    api_dns                    = module.main_domain[0].api_dns_record
  } : null
}

output "click_domain_summary" {
  description = "Click domain summary"
  value = length(module.click_domain) > 0 ? {
    domain                     = var.click_domain
    zone_id                    = module.click_domain[0].zone_id
    regional_certificate_arn   = module.click_domain[0].regional_certificate_arn
    cloudfront_certificate_arn = module.click_domain[0].cloudfront_certificate_arn
    click                      = module.click_domain[0].click
    cdn                        = module.click_domain[0].cdn
    reports                    = module.click_domain[0].reports
    root_alb_dns               = module.click_domain[0].root_alb_dns_record
  } : null
}

output "deployment_summary" {
  description = "Complete deployment summary"
  value = {
    wl_type      = var.wl_type
    alb_dns_name = var.alb_dns_name
    main_domain  = length(module.main_domain) > 0 ? module.main_domain[0] : null
    click_domain = length(module.click_domain) > 0 ? module.click_domain[0] : null
  }
}```
----------------------------

----------------------------
# terraform/environments/prod/main.tf

```
# terraform/environments/prod/main.tf

locals {
  common_tags = {
    Project     = "WL-Automation"
    Environment = "production"
    ManagedBy   = "Terraform"
  }
}

# ============================================================================
# Main Domain
# ============================================================================
module "main_domain" {
  count  = var.domain != null ? 1 : 0
  source = "../../modules/wl-domain"

  domain             = var.domain
  sans               = var.sans
  cloudflare_zone_id = var.cloudflare_zone_id
  wl_type            = var.wl_type
  platform_code      = var.platform_code

  # Subdomain configuration
  admin_subdomain = var.admin_subdomain
  agent_subdomain = var.agent_subdomain

  # Resource creation logic based on wl_type
  create_admin = true
  create_agent = contains(["agent", "mixed"], var.wl_type)
  create_click = var.wl_type == "mixed"

  # ALB DNS configuration
  alb_dns_name      = var.alb_dns_name
  create_api_record = true  # Always create api.{domain} for main domain

  # GitLab CI/CD
  gitlab_project_id          = var.gitlab_project_id
  gitlab_variables_protected = false
  gitlab_variables_masked    = false
  gitlab_environment_scope   = "*"

  tags = merge(
    local.common_tags,
    {
      WL_Name = split(".", var.domain)[0]
      Domain  = var.domain
      WL_Type = var.wl_type
    }
  )

  providers = {
    aws        = aws
    aws.east   = aws.east
    cloudflare = cloudflare
  }
}

# ============================================================================
# Separate Click Domain (for Click/Mixed WL types)
# ============================================================================
module "click_domain" {
  count  = var.click_domain != null ? 1 : 0
  source = "../../modules/wl-domain"

  domain             = var.click_domain
  cloudflare_zone_id = var.click_zone_id
  wl_type            = "click"
  platform_code      = var.platform_code

  create_admin = false
  create_agent = false
  create_click = true

  # ALB DNS configuration (click domain needs root record only)
  alb_dns_name       = var.alb_dns_name
  create_api_record  = false  # No api.{click_domain}
  create_root_record = true   # Create {click_domain} → ALB

  # NO GitLab variables for separate click domain
  gitlab_project_id          = null
  gitlab_variables_protected = false
  gitlab_variables_masked    = false
  gitlab_environment_scope   = "*"

  tags = merge(
    local.common_tags,
    {
      WL_Name = split(".", var.click_domain)[0]
      Domain  = var.click_domain
      WL_Type = "click"
    }
  )

  providers = {
    aws        = aws
    aws.east   = aws.east
    cloudflare = cloudflare
  }
}```
----------------------------

----------------------------
# terraform/environments/prod/versions.tf

```
# terraform/environments/prod/versions.tf
terraform {
  required_version = ">= 1.5.0"
}
```
----------------------------

----------------------------
# terraform/environments/prod/providers.tf

```
# terraform/environments/prod/providers.tf

terraform {
  required_version = ">= 1.5.0"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 6.0"
    }
    cloudflare = {
      source  = "cloudflare/cloudflare"
      version = "~> 5.0"
    }
    gitlab = {
      source  = "gitlabhq/gitlab"
      version = "~> 17.0"
    }
  }
}

# Default AWS Provider (eu-central-1)
provider "aws" {
  region = "eu-central-1"

  default_tags {
    tags = {
      Project     = "WL-Automation"
      ManagedBy   = "Terraform"
      Environment = "production"
    }
  }
}

# AWS Provider for us-east-1 (CloudFront requirement)
provider "aws" {
  alias  = "east"
  region = "us-east-1"

  default_tags {
    tags = {
      Project     = "WL-Automation"
      ManagedBy   = "Terraform"
      Environment = "production"
    }
  }
}

# Cloudflare Provider
provider "cloudflare" {
  api_token = var.cloudflare_api_token
}

# GitLab Provider
provider "gitlab" {
  token    = var.gitlab_token
  base_url = var.gitlab_base_url
}```
----------------------------

----------------------------
# terraform/environments/prod/variables.tf

```
# terraform/environments/prod/variables.tf

variable "cloudflare_api_token" {
  description = "Cloudflare API token"
  type        = string
  sensitive   = true
}

variable "cloudflare_account_id" {
  description = "Cloudflare Account ID"
  type        = string
  default     = null
}

variable "domain" {
  description = "Primary domain"
  type        = string
  default     = null
}

variable "sans" {
  description = "Subject Alternative Names"
  type        = list(string)
  default     = null
}

variable "wl_type" {
  description = "WL type: agent, click, or mixed"
  type        = string
  default     = "agent"

  validation {
    condition     = contains(["agent", "click", "mixed"], var.wl_type)
    error_message = "Must be agent, click, or mixed."
  }
}

variable "click_domain" {
  description = "Click domain for separate click domain (root domain only)"
  type        = string
  default     = null
}

variable "cloudflare_zone_id" {
  description = "Cloudflare Zone ID"
  type        = string
  default     = null
}

variable "click_zone_id" {
  description = "Cloudflare Zone ID for click domain"
  type        = string
  default     = null
}

variable "platform_code" {
  description = "Platform code (e.g., 'LIRV', 'AFFTECH') - REQUIRED"
  type        = string

  validation {
    condition     = var.platform_code != null && length(var.platform_code) > 0
    error_message = "platform_code is required. Example: 'LIRV', 'SLPKNG', 'AFFTECH'"
  }
}

# ============================================================================
# Subdomain Configuration
# ============================================================================

variable "admin_subdomain" {
  description = "Admin subdomain (from Confluence Admin Domain)"
  type        = string
  default     = "admin"
}

variable "agent_subdomain" {
  description = "Agent subdomain (from Confluence Agent Domain)"
  type        = string
  default     = "agent"
}

# ============================================================================
# GitLab Configuration
# ============================================================================

variable "gitlab_token" {
  description = "GitLab API token (required for GitLab integration)"
  type        = string
  sensitive   = true
  default     = null
}

variable "gitlab_base_url" {
  description = "GitLab base URL"
  type        = string
  default     = "https://git.betlab.com/api/v4"
}

variable "gitlab_project_id" {
  description = "GitLab project ID or path"
  type        = string
  default     = null
}

# ============================================================================
# ALB Configuration
# ============================================================================

variable "alb_dns_name" {
  description = "ALB DNS name for API ingress"
  type        = string
  default     = "mt-apps-ingress-978b1006d8a9d559.elb.eu-central-1.amazonaws.com"
}```
----------------------------

----------------------------
# terraform/environments/prod/backend.tf

```
# terraform/environments/prod/backend.tf

# terraform {
#   backend "s3" {
#     bucket         = "terraform-state-marktech"
#     key            = "wl-automation/terraform.tfstate"
#     region         = "eu-central-1"
#     encrypt        = true
#     dynamodb_table = "terraform-state-lock"
#   }
# }
```
----------------------------

----------------------------
# terraform/modules/cloudfront-s3-website/outputs.tf

```
# terraform/modules/cloudfront-s3-website/outputs.tf

# ============================================================================
# General Outputs
# ============================================================================

output "fqdn" {
  description = "Full domain name (e.g., admin.afftech.xyz)"
  value       = local.fqdn
}

# ============================================================================
# S3 Outputs
# ============================================================================

output "s3_bucket_id" {
  description = "S3 bucket ID"
  value       = aws_s3_bucket.website.id
}

output "s3_bucket_name" {
  description = "S3 bucket name"
  value       = aws_s3_bucket.website.bucket
}

output "s3_bucket_arn" {
  description = "S3 bucket ARN"
  value       = aws_s3_bucket.website.arn
}

output "s3_bucket_regional_domain_name" {
  description = "S3 bucket regional domain name"
  value       = aws_s3_bucket.website.bucket_regional_domain_name
}

output "s3_website_endpoint" {
  description = "S3 website endpoint"
  value       = aws_s3_bucket_website_configuration.website.website_endpoint
}

# ============================================================================
# CloudFront Outputs
# ============================================================================

output "cloudfront_distribution_id" {
  description = "CloudFront distribution ID"
  value       = aws_cloudfront_distribution.website.id
}

output "cloudfront_distribution_arn" {
  description = "CloudFront distribution ARN"
  value       = aws_cloudfront_distribution.website.arn
}

output "cloudfront_domain_name" {
  description = "CloudFront domain name (e.g., d111111abcdef8.cloudfront.net)"
  value       = aws_cloudfront_distribution.website.domain_name
}

output "cloudfront_hosted_zone_id" {
  description = "CloudFront hosted zone ID (for Route53/DNS)"
  value       = aws_cloudfront_distribution.website.hosted_zone_id
}

output "cloudfront_status" {
  description = "CloudFront distribution status"
  value       = aws_cloudfront_distribution.website.status
}

# ============================================================================
# OAI Outputs
# ============================================================================

output "oai_id" {
  description = "Origin Access Identity ID"
  value       = aws_cloudfront_origin_access_identity.website.id
}

output "oai_iam_arn" {
  description = "Origin Access Identity IAM ARN"
  value       = aws_cloudfront_origin_access_identity.website.iam_arn
}```
----------------------------

----------------------------
# terraform/modules/cloudfront-s3-website/main.tf

```
# terraform/modules/cloudfront-s3-website/main.tf

# ============================================================================
# Local Variables
# ============================================================================

locals {
  # Support both subdomain and root domain
  # subdomain = "@" → root domain (e.g., "trackingslapkong.com")
  # subdomain = "admin" → subdomain (e.g., "admin.afftech.xyz")
  fqdn = var.subdomain == "@" ? var.domain_name : "${var.subdomain}.${var.domain_name}"
}

# ============================================================================
# S3 Bucket
# ============================================================================

resource "aws_s3_bucket" "website" {
  bucket = local.fqdn

  tags = merge(
    var.tags,
    {
      Name      = local.fqdn
      Subdomain = var.subdomain
      Domain    = var.domain_name
    }
  )
}

# ============================================================================
# S3 Bucket Website Configuration
# ============================================================================

resource "aws_s3_bucket_website_configuration" "website" {
  bucket = aws_s3_bucket.website.id

  index_document {
    suffix = var.s3_index_document
  }

  error_document {
    key = var.s3_error_document
  }
}

# ============================================================================
# S3 Bucket Versioning
# ============================================================================

resource "aws_s3_bucket_versioning" "website" {
  bucket = aws_s3_bucket.website.id

  versioning_configuration {
    status = var.enable_versioning ? "Enabled" : "Suspended"
  }
}

# ============================================================================
# S3 Bucket Public Access Block
# ============================================================================

resource "aws_s3_bucket_public_access_block" "website" {
  bucket = aws_s3_bucket.website.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# ============================================================================
# CloudFront Origin Access Identity (OAI)
# ============================================================================

resource "aws_cloudfront_origin_access_identity" "website" {
  comment = "${local.fqdn}-oai"
}

# ============================================================================
# S3 Bucket Policy (Allow CloudFront Access)
# ============================================================================

data "aws_iam_policy_document" "s3_cloudfront_access" {
  statement {
    sid    = "AllowCloudFrontAccess"
    effect = "Allow"

    principals {
      type        = "AWS"
      identifiers = [aws_cloudfront_origin_access_identity.website.iam_arn]
    }

    actions = [
      "s3:GetObject"
    ]

    resources = [
      "${aws_s3_bucket.website.arn}/*"
    ]
  }
}

resource "aws_s3_bucket_policy" "website" {
  bucket = aws_s3_bucket.website.id
  policy = data.aws_iam_policy_document.s3_cloudfront_access.json
  depends_on = [aws_cloudfront_origin_access_identity.website]
}

# ============================================================================
# CloudFront Distribution
# ============================================================================

resource "aws_cloudfront_distribution" "website" {
  enabled             = true
  is_ipv6_enabled     = true
  comment             = "CloudFront distribution for ${local.fqdn}"
  default_root_object = "index.html"
  aliases             = [local.fqdn]
  price_class         = var.price_class

  # Origin (S3)
  origin {
    domain_name = aws_s3_bucket.website.bucket_regional_domain_name
    origin_id   = "S3-${local.fqdn}"

    s3_origin_config {
      origin_access_identity = aws_cloudfront_origin_access_identity.website.cloudfront_access_identity_path
    }
  }

  # Default cache behavior
  default_cache_behavior {
    target_origin_id       = "S3-${local.fqdn}"
    viewer_protocol_policy = var.viewer_protocol_policy
    allowed_methods        = ["GET", "HEAD", "OPTIONS"]
    cached_methods         = ["GET", "HEAD"]
    compress               = var.enable_compression

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }

    min_ttl     = var.min_ttl
    default_ttl = var.default_ttl
    max_ttl     = var.max_ttl
  }

  # Custom error responses
  dynamic "custom_error_response" {
    for_each = var.custom_error_responses

    content {
      error_code         = custom_error_response.value.error_code
      response_code      = custom_error_response.value.response_code
      response_page_path = custom_error_response.value.response_page_path
    }
  }

  # SSL certificate
  viewer_certificate {
    acm_certificate_arn      = var.certificate_arn
    ssl_support_method       = "sni-only"
    minimum_protocol_version = "TLSv1.2_2021"
  }

  # Geo restrictions (none)
  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  tags = merge(
    var.tags,
    {
      Name      = local.fqdn
      Subdomain = var.subdomain
      Domain    = var.domain_name
    }
  )
}```
----------------------------

----------------------------
# terraform/modules/cloudfront-s3-website/versions.tf

```
# terraform/modules/cloudfront-s3-website/versions.tf

terraform {
  required_version = ">= 1.5.0"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 6.0"
    }
  }
}```
----------------------------

----------------------------
# terraform/modules/cloudfront-s3-website/variables.tf

```
# terraform/modules/cloudfront-s3-website/variables.tf

# ============================================================================
# Core Configuration
# ============================================================================

variable "domain_name" {
  description = "Primary domain name (e.g., 'afftech.xyz')"
  type        = string

  validation {
    condition     = can(regex("^[a-z0-9][a-z0-9.-]*[a-z0-9]\\.[a-z]{2,}$", var.domain_name))
    error_message = "Domain must be valid format (e.g., 'example.com')."
  }
}

variable "subdomain" {
  description = "Subdomain prefix (e.g., 'admin' for admin.afftech.xyz) or '@' for root domain"
  type        = string

  validation {
    condition     = can(regex("^(@|[a-z0-9-]+)$", var.subdomain))
    error_message = "Subdomain must contain only lowercase letters, numbers, hyphens, or '@' for root domain."
  }
}

variable "certificate_arn" {
  description = "ACM certificate ARN (must be in us-east-1 for CloudFront)"
  type        = string

  validation {
    condition     = can(regex("^arn:aws:acm:us-east-1:[0-9]{12}:certificate/[a-f0-9-]+$", var.certificate_arn))
    error_message = "Certificate ARN must be valid us-east-1 ACM certificate."
  }
}

# ============================================================================
# S3 Configuration
# ============================================================================

variable "s3_index_document" {
  description = "S3 website index document"
  type        = string
  default     = "index.html"
}

variable "s3_error_document" {
  description = "S3 website error document"
  type        = string
  default     = "index.html"
}

variable "enable_versioning" {
  description = "Enable S3 versioning"
  type        = bool
  default     = false
}

# ============================================================================
# CloudFront Configuration
# ============================================================================

variable "enable_compression" {
  description = "Enable CloudFront compression"
  type        = bool
  default     = true
}

variable "viewer_protocol_policy" {
  description = "Viewer protocol policy"
  type        = string
  default     = "redirect-to-https"

  validation {
    condition     = contains(["allow-all", "https-only", "redirect-to-https"], var.viewer_protocol_policy)
    error_message = "Must be 'allow-all', 'https-only', or 'redirect-to-https'."
  }
}

variable "price_class" {
  description = "CloudFront price class"
  type        = string
  default     = "PriceClass_All"

  validation {
    condition     = contains(["PriceClass_All", "PriceClass_200", "PriceClass_100"], var.price_class)
    error_message = "Must be valid price class."
  }
}

variable "min_ttl" {
  description = "Minimum TTL for CloudFront cache"
  type        = number
  default     = 0
}

variable "default_ttl" {
  description = "Default TTL for CloudFront cache"
  type        = number
  default     = 3600
}

variable "max_ttl" {
  description = "Maximum TTL for CloudFront cache"
  type        = number
  default     = 86400
}

variable "custom_error_responses" {
  description = "Custom error responses for CloudFront"
  type = list(object({
    error_code         = number
    response_code      = number
    response_page_path = string
  }))
  default = [
    {
      error_code         = 403
      response_code      = 200
      response_page_path = "/index.html"
    }
  ]
}

# ============================================================================
# Tags
# ============================================================================

variable "tags" {
  description = "Resource tags"
  type        = map(string)
  default     = {}
}```
----------------------------

----------------------------
# terraform/modules/gitlab-ci-variables/outputs.tf

```
# terraform/modules/gitlab-ci-variables/outputs.tf

output "admin_variable" {
  description = "Admin GitLab CI/CD variable details"
  value = {
    key   = gitlab_project_variable.admin_bucket.key
    value = gitlab_project_variable.admin_bucket.value
  }
  sensitive = true
}

output "agent_variable" {
  description = "Agent GitLab CI/CD variable details"
  value = contains(["agent", "mixed"], var.wl_type) ? {
    key   = gitlab_project_variable.agent_bucket[0].key
    value = gitlab_project_variable.agent_bucket[0].value
  } : null
  sensitive = true
}

output "platform_code" {
  description = "Platform code (uppercase)"
  value       = upper(var.platform_code)
}```
----------------------------

----------------------------
# terraform/modules/gitlab-ci-variables/main.tf

```
# terraform/modules/gitlab-ci-variables/main.tf

# ============================================================================
# GitLab CI/CD Variables
# ============================================================================

locals {
  platform_code = upper(var.platform_code)
}

resource "gitlab_project_variable" "admin_bucket" {
  project           = var.gitlab_project_id
  key               = "${local.platform_code}_PROD_BUCKET_NAME"
  value             = "${var.admin_subdomain}.${var.domain}"
  protected         = var.protected
  masked            = var.masked
  environment_scope = var.environment_scope
  description       = "Admin bucket for ${var.domain} (${var.wl_type} WL)"
}

resource "gitlab_project_variable" "agent_bucket" {
    count = contains(["agent", "mixed"], var.wl_type) ? 1 : 0

  project           = var.gitlab_project_id
  key               = "${local.platform_code}_AGENT_PROD_BUCKET_NAME"
  value             = "${var.agent_subdomain}.${var.domain}"
  protected         = var.protected
  masked            = var.masked
  environment_scope = var.environment_scope
  description       = "Agent bucket for ${var.domain} (${var.wl_type} WL)"
}```
----------------------------

----------------------------
# terraform/modules/gitlab-ci-variables/versions.tf

```
# terraform/modules/gitlab-ci-variables/versions.tf

terraform {
  required_version = ">= 1.5.0"

  required_providers {
    gitlab = {
      source  = "gitlabhq/gitlab"
      version = "~> 17.0"
    }
  }
}```
----------------------------

----------------------------
# terraform/modules/gitlab-ci-variables/variables.tf

```
# terraform/modules/gitlab-ci-variables/variables.tf

variable "gitlab_project_id" {
  description = "GitLab project ID (e.g., 'marketingtech/pmaffiliate/pmaffiliate-react-front')"
  type        = string
}

variable "domain" {
  description = "Domain name (e.g., 'liravegas.com')"
  type        = string
}

variable "wl_type" {
  description = "WL type: agent, click, or mixed"
  type        = string
  default     = "agent"

  validation {
    condition     = contains(["agent", "click", "mixed"], var.wl_type)
    error_message = "Must be 'agent', 'click', or 'mixed'."
  }
}

variable "platform_code" {
  description = "Platform code (e.g., 'LIRV') - REQUIRED"
  type        = string

  validation {
    condition     = length(var.platform_code) > 0
    error_message = "platform_code cannot be empty."
  }
}

variable "admin_subdomain" {
  description = "Admin subdomain (e.g., 'admin', 'adminagent')"
  type        = string
}

variable "agent_subdomain" {
  description = "Agent subdomain (e.g., 'agent')"
  type        = string
  default     = "agent"
}

variable "protected" {
  description = "Whether the variable is protected"
  type        = bool
  default     = false
}

variable "masked" {
  description = "Whether the variable is masked"
  type        = bool
  default     = false
}

variable "environment_scope" {
  description = "Environment scope (* for all environments)"
  type        = string
  default     = "*"
}```
----------------------------

----------------------------
# terraform/modules/wl-domain/outputs.tf

```
# terraform/modules/wl-domain/outputs.tf

output "zone_id" {
  description = "Cloudflare Zone ID"
  value       = local.zone_id
}

output "regional_certificate_arn" {
  description = "Regional certificate ARN"
  value       = module.validation.regional_certificate_arn
}

output "cloudfront_certificate_arn" {
  description = "CloudFront certificate ARN"
  value       = module.validation.cloudfront_certificate_arn
}

output "admin" {
  description = "Admin distribution details"
  value = var.create_admin ? {
    fqdn               = module.admin[0].fqdn
    s3_bucket          = module.admin[0].s3_bucket_name
    cloudfront_domain  = module.admin[0].cloudfront_domain_name
    cloudfront_id      = module.admin[0].cloudfront_distribution_id
  } : null
}

output "agent" {
  description = "Agent distribution details"
  value = var.create_agent ? {
    fqdn               = module.agent[0].fqdn
    s3_bucket          = module.agent[0].s3_bucket_name
    cloudfront_domain  = module.agent[0].cloudfront_domain_name
    cloudfront_id      = module.agent[0].cloudfront_distribution_id
  } : null
}

output "click" {
  description = "Click distribution details"
  value = var.create_click ? {
    fqdn               = module.click[0].fqdn
    s3_bucket          = module.click[0].s3_bucket_name
    cloudfront_domain  = module.click[0].cloudfront_domain_name
    cloudfront_id      = module.click[0].cloudfront_distribution_id
  } : null
}

output "cdn" {
  description = "CDN distribution details"
  value = var.create_cdn ? {
    fqdn               = module.cdn[0].fqdn
    s3_bucket          = module.cdn[0].s3_bucket_name
    cloudfront_domain  = module.cdn[0].cloudfront_domain_name
    cloudfront_id      = module.cdn[0].cloudfront_distribution_id
  } : null
}

output "reports" {
  description = "Reports distribution details"
  value = var.create_reports ? {
    fqdn               = module.reports[0].fqdn
    s3_bucket          = module.reports[0].s3_bucket_name
    cloudfront_domain  = module.reports[0].cloudfront_domain_name
    cloudfront_id      = module.reports[0].cloudfront_distribution_id
  } : null
}

# ============================================================================
# ALB DNS Outputs
# ============================================================================

output "api_dns_record" {
  description = "API DNS record details"
  value = var.create_api_record ? {
    fqdn    = "api.${var.domain}"
    target  = var.alb_dns_name
    proxied = true
  } : null
}

output "root_alb_dns_record" {
  description = "Root domain ALB DNS record (for click domains)"
  value = var.create_root_record ? {
    fqdn    = var.domain
    target  = var.alb_dns_name
    proxied = true
  } : null
}```
----------------------------

----------------------------
# terraform/modules/wl-domain/main.tf

```
# terraform/modules/wl-domain/main.tf

# ============================================================================
# Cloudflare Zone Lookup
# ============================================================================
data "cloudflare_zone" "zone" {
  filter = {
    name = var.domain
  }
}

locals {
  zone_id = "84787ea66aa226406e7c736892c6d493"  # afftech.xyz
  sans    = var.sans != null && length(var.sans) > 0 ? var.sans : ["*.${var.domain}"]
}

# ============================================================================
# ACM Certificates (eu-central-1 + us-east-1)
# ============================================================================
module "acm" {
  source = "../acm-certificates"

  domain_name               = var.domain
  subject_alternative_names = local.sans

  create_regional_cert   = var.create_regional_cert
  create_cloudfront_cert = var.create_cloudfront_cert

  tags = var.tags

  providers = {
    aws      = aws
    aws.east = aws.east
  }
}

# ============================================================================
# DNS Validation
# ============================================================================
module "validation" {
  source = "../acm-dns-validation"

  domain_name        = var.domain
  cloudflare_zone_id = local.zone_id

  regional_certificate   = module.acm.regional_certificate
  cloudfront_certificate = module.acm.cloudfront_certificate

  validation_timeout = var.validation_timeout

  providers = {
    aws        = aws
    aws.east   = aws.east
    cloudflare = cloudflare
  }

  depends_on = [module.acm]
}

# ============================================================================
# Admin CloudFront + S3
# ============================================================================
module "admin" {
  count  = var.create_admin ? 1 : 0
  source = "../cloudfront-s3-website"

  domain_name     = var.domain
  subdomain       = var.admin_subdomain
  certificate_arn = module.validation.cloudfront_certificate_arn

  s3_index_document      = var.s3_index_document
  s3_error_document      = var.s3_error_document
  enable_versioning      = var.enable_versioning
  enable_compression     = var.enable_compression
  viewer_protocol_policy = var.viewer_protocol_policy
  price_class            = var.price_class
  min_ttl                = var.min_ttl
  default_ttl            = var.default_ttl
  max_ttl                = var.max_ttl
  custom_error_responses = var.custom_error_responses

  tags = merge(var.tags, {
    Subdomain = var.admin_subdomain
    Purpose   = "Admin Panel"
  })

  depends_on = [module.validation]
}

# ============================================================================
# Agent CloudFront + S3
# ============================================================================
module "agent" {
  count  = var.create_agent ? 1 : 0
  source = "../cloudfront-s3-website"

  domain_name     = var.domain
  subdomain       = var.agent_subdomain
  certificate_arn = module.validation.cloudfront_certificate_arn

  s3_index_document      = var.s3_index_document
  s3_error_document      = var.s3_error_document
  enable_versioning      = var.enable_versioning
  enable_compression     = var.enable_compression
  viewer_protocol_policy = var.viewer_protocol_policy
  price_class            = var.price_class
  min_ttl                = var.min_ttl
  default_ttl            = var.default_ttl
  max_ttl                = var.max_ttl
  custom_error_responses = var.custom_error_responses

  tags = merge(var.tags, {
    Subdomain = var.agent_subdomain
    Purpose   = "Agent Panel"
  })

  depends_on = [module.validation]
}

# ============================================================================
# Click Root Domain CloudFront + S3
# Uses "@" to indicate root domain (no subdomain)
# ============================================================================
module "click" {
  count  = var.create_click ? 1 : 0
  source = "../cloudfront-s3-website"

  domain_name     = var.domain
  subdomain       = "@"  # Root domain indicator
  certificate_arn = module.validation.cloudfront_certificate_arn

  s3_index_document      = var.s3_index_document
  s3_error_document      = var.s3_error_document
  enable_versioning      = var.enable_versioning
  enable_compression     = var.enable_compression
  viewer_protocol_policy = var.viewer_protocol_policy
  price_class            = var.price_class
  min_ttl                = var.min_ttl
  default_ttl            = var.default_ttl
  max_ttl                = var.max_ttl
  custom_error_responses = var.custom_error_responses

  tags = merge(var.tags, {
    Subdomain = "@"
    Purpose   = "Click Domain (Root)"
  })

  depends_on = [module.validation]
}

# ============================================================================
# CDN CloudFront + S3
# ============================================================================
module "cdn" {
  count  = var.create_cdn ? 1 : 0
  source = "../cloudfront-s3-website"

  domain_name     = var.domain
  subdomain       = var.cdn_subdomain
  certificate_arn = module.validation.cloudfront_certificate_arn

  # CDN-specific configuration
  s3_index_document      = "index.html"
  s3_error_document      = "404.html"
  enable_versioning      = false
  enable_compression     = true
  viewer_protocol_policy = "redirect-to-https"
  price_class            = var.price_class

  # CDN cache settings - Long TTL for static assets
  min_ttl     = 0
  default_ttl = var.cdn_default_ttl
  max_ttl     = var.cdn_max_ttl

  custom_error_responses = [
    {
      error_code         = 403
      response_code      = 404
      response_page_path = "/404.html"
    },
    {
      error_code         = 404
      response_code      = 404
      response_page_path = "/404.html"
    }
  ]

  tags = merge(var.tags, {
    Subdomain = var.cdn_subdomain
    Purpose   = "CDN Static Assets"
  })

  depends_on = [module.validation]
}

# ============================================================================
# Reports CloudFront + S3
# ============================================================================
module "reports" {
  count  = var.create_reports ? 1 : 0
  source = "../cloudfront-s3-website"

  domain_name     = var.domain
  subdomain       = var.reports_subdomain
  certificate_arn = module.validation.cloudfront_certificate_arn

  # Reports-specific configuration
  s3_index_document      = "index.html"
  s3_error_document      = "error.html"
  enable_versioning      = var.reports_enable_versioning
  enable_compression     = true
  viewer_protocol_policy = "redirect-to-https"
  price_class            = var.reports_price_class

  # Reports cache settings - Medium TTL
  min_ttl     = 0
  default_ttl = 3600   # 1 hour
  max_ttl     = 86400  # 24 hours

  custom_error_responses = [
    {
      error_code         = 403
      response_code      = 200
      response_page_path = "/index.html"
    },
    {
      error_code         = 404
      response_code      = 200
      response_page_path = "/index.html"
    }
  ]

  tags = merge(var.tags, {
    Subdomain = var.reports_subdomain
    Purpose   = "Reports Portal"
  })

  depends_on = [module.validation]
}

# ============================================================================
# DNS Records
# ============================================================================

# Admin CNAME
resource "cloudflare_dns_record" "admin" {
  count   = var.create_admin ? 1 : 0
  zone_id = local.zone_id

  name    = module.admin[0].fqdn
  type    = "CNAME"
  content = module.admin[0].cloudfront_domain_name
  ttl     = 1
  proxied = false

  comment = "Admin → CloudFront"
}

# Agent CNAME
resource "cloudflare_dns_record" "agent" {
  count   = var.create_agent ? 1 : 0
  zone_id = local.zone_id

  name    = module.agent[0].fqdn
  type    = "CNAME"
  content = module.agent[0].cloudfront_domain_name
  ttl     = 1
  proxied = false

  comment = "Agent → CloudFront"
}

# Click CNAME (Root Domain)
resource "cloudflare_dns_record" "click" {
  count   = var.create_click ? 1 : 0
  zone_id = local.zone_id

  name    = module.click[0].fqdn
  type    = "CNAME"
  content = module.click[0].cloudfront_domain_name
  ttl     = 1
  proxied = false

  comment = "Click (Root) → CloudFront"
}

# CDN CNAME
resource "cloudflare_dns_record" "cdn" {
  count   = var.create_cdn ? 1 : 0
  zone_id = local.zone_id

  name    = module.cdn[0].fqdn
  type    = "CNAME"
  content = module.cdn[0].cloudfront_domain_name
  ttl     = 1
  proxied = false

  comment = "CDN → CloudFront"
}

# Reports CNAME
resource "cloudflare_dns_record" "reports" {
  count   = var.create_reports ? 1 : 0
  zone_id = local.zone_id

  name    = module.reports[0].fqdn
  type    = "CNAME"
  content = module.reports[0].cloudfront_domain_name
  ttl     = 1
  proxied = false

  comment = "Reports → CloudFront"
}

# ============================================================================
# DNS Records - ALB
# ============================================================================

# API CNAME Record (api.{domain} → ALB)
# Created for: Agent, Click, Mixed WL (main domain only)
resource "cloudflare_dns_record" "api" {
  count   = var.create_api_record ? 1 : 0
  zone_id = local.zone_id

  name    = "api.${var.domain}"
  type    = "CNAME"
  content = var.alb_dns_name
  ttl     = 1
  proxied = true

  comment = "API → ALB (${var.wl_type} WL)"
}

# Root Domain CNAME Record ({domain} → ALB)
# Created for: Click domain (separate tracking domain)
# Example: trackingslapkong.com → ALB
resource "cloudflare_dns_record" "root_alb" {
  count   = var.create_root_record ? 1 : 0
  zone_id = local.zone_id

  name    = var.domain
  type    = "CNAME"
  content = var.alb_dns_name
  ttl     = 1
  proxied = true

  comment = "Click Domain (Root) → ALB"
}

# ============================================================================
# GitLab CI/CD Variables (Optional)
# ============================================================================

module "gitlab_variables" {
  count  = var.gitlab_project_id != null ? 1 : 0
  source = "../gitlab-ci-variables"

  gitlab_project_id = var.gitlab_project_id
  domain            = var.domain
  wl_type           = var.wl_type
  platform_code     = var.platform_code
  admin_subdomain   = var.admin_subdomain
  agent_subdomain   = var.agent_subdomain

  protected         = var.gitlab_variables_protected
  masked            = var.gitlab_variables_masked
  environment_scope = var.gitlab_environment_scope

  depends_on = [
    module.admin,
    module.agent
  ]
}```
----------------------------

----------------------------
# terraform/modules/wl-domain/versions.tf

```
# terraform/modules/wl-domain/versions.tf

terraform {
  required_version = ">= 1.5.0"

  required_providers {
    aws = {
      source                = "hashicorp/aws"
      version               = "~> 6.0"
      configuration_aliases = [aws.east]
    }
    cloudflare = {
      source  = "cloudflare/cloudflare"
      version = "~> 5.0"
    }
  }
}```
----------------------------

----------------------------
# terraform/modules/wl-domain/variables.tf

```
# terraform/modules/wl-domain/variables.tf

variable "domain" {
  description = "Domain name"
  type        = string

  validation {
    condition     = can(regex("^[a-z0-9][a-z0-9.-]*[a-z0-9]\\.[a-z]{2,}$", var.domain))
    error_message = "Domain must be valid format."
  }
}

variable "sans" {
  description = "Subject Alternative Names"
  type        = list(string)
  default     = []
}

variable "create_regional_cert" {
  description = "Create regional certificate"
  type        = bool
  default     = true
}

variable "create_cloudfront_cert" {
  description = "Create CloudFront certificate"
  type        = bool
  default     = true
}

variable "create_admin" {
  description = "Create admin CloudFront"
  type        = bool
  default     = true
}

variable "create_agent" {
  description = "Create agent CloudFront"
  type        = bool
  default     = false
}

variable "create_click" {
  description = "Create click subdomain CloudFront"
  type        = bool
  default     = false
}

# ============================================================================
# Subdomain Configuration (from Confluence)
# ============================================================================

variable "admin_subdomain" {
  description = "Admin subdomain (from Confluence Admin Domain field, e.g., 'admin', 'adminagent')"
  type        = string
  default     = "admin"

  validation {
    condition     = can(regex("^[a-z0-9-]+$", var.admin_subdomain))
    error_message = "Admin subdomain must contain only lowercase letters, numbers, and hyphens."
  }
}

variable "agent_subdomain" {
  description = "Agent subdomain (from Confluence Agent Domain field, e.g., 'agent')"
  type        = string
  default     = "agent"

  validation {
    condition     = can(regex("^[a-z0-9-]+$", var.agent_subdomain))
    error_message = "Agent subdomain must contain only lowercase letters, numbers, and hyphens."
  }
}

variable "validation_timeout" {
  description = "ACM validation timeout"
  type        = string
  default     = "45m"
}

variable "s3_index_document" {
  type    = string
  default = "index.html"
}

variable "s3_error_document" {
  type    = string
  default = "index.html"
}

variable "enable_versioning" {
  type    = bool
  default = false
}

variable "enable_compression" {
  type    = bool
  default = true
}

variable "viewer_protocol_policy" {
  type    = string
  default = "redirect-to-https"
}

variable "price_class" {
  type    = string
  default = "PriceClass_All"
}

variable "min_ttl" {
  type    = number
  default = 0
}

variable "default_ttl" {
  type    = number
  default = 3600
}

variable "max_ttl" {
  type    = number
  default = 86400
}

variable "custom_error_responses" {
  type = list(object({
    error_code         = number
    response_code      = number
    response_page_path = string
  }))
  default = [
    {
      error_code         = 403
      response_code      = 200
      response_page_path = "/index.html"
    }
  ]
}

variable "tags" {
  description = "Resource tags"
  type        = map(string)
  default     = {}
}

variable "cloudflare_account_id" {
  description = "Cloudflare Account ID (if you have multiple accounts)"
  type        = string
  default     = null
}

variable "cloudflare_zone_id" {
  description = "Cloudflare Zone ID"
  type        = string
  default     = null
}

# ============================================================================
# CDN and Reports Configuration
# ============================================================================

variable "create_cdn" {
  description = "Create CDN CloudFront distribution"
  type        = bool
  default     = true
}

variable "create_reports" {
  description = "Create Reports CloudFront distribution"
  type        = bool
  default     = true
}

variable "cdn_subdomain" {
  description = "CDN subdomain name"
  type        = string
  default     = "cdn"
}

variable "reports_subdomain" {
  description = "Reports subdomain name"
  type        = string
  default     = "reports"
}

# CDN-specific cache settings
variable "cdn_default_ttl" {
  description = "Default TTL for CDN cache (in seconds)"
  type        = number
  default     = 86400  # 24 hours
}

variable "cdn_max_ttl" {
  description = "Maximum TTL for CDN cache (in seconds)"
  type        = number
  default     = 31536000  # 1 year
}

# Reports-specific settings
variable "reports_enable_versioning" {
  description = "Enable versioning for Reports S3 bucket"
  type        = bool
  default     = true
}

variable "reports_price_class" {
  description = "CloudFront price class for Reports"
  type        = string
  default     = "PriceClass_100"  # Cheaper, reports have less traffic
}

# ============================================================================
# WL Configuration
# ============================================================================

variable "wl_type" {
  description = "WL type: agent, click, or mixed"
  type        = string
  default     = "agent"

  validation {
    condition     = contains(["agent", "click", "mixed"], var.wl_type)
    error_message = "Must be 'agent', 'click', or 'mixed'."
  }
}

variable "platform_code" {
  description = "Platform code (e.g., 'LIRV', 'SLPKNG') - REQUIRED"
  type        = string

  validation {
    condition     = var.platform_code != null && length(var.platform_code) > 0
    error_message = "platform_code is required. Example: 'LIRV', 'SLPKNG', 'AFFTECH'"
  }
}

# ============================================================================
# GitLab CI/CD Configuration
# ============================================================================

variable "gitlab_project_id" {
  description = "GitLab project ID (null to skip GitLab integration)"
  type        = string
  default     = null
}

variable "gitlab_variables_protected" {
  description = "Whether GitLab variables are protected"
  type        = bool
  default     = false
}

variable "gitlab_variables_masked" {
  description = "Whether GitLab variables are masked"
  type        = bool
  default     = false
}

variable "gitlab_environment_scope" {
  description = "GitLab environment scope"
  type        = string
  default     = "*"
}

# ============================================================================
# ALB Configuration
# ============================================================================

variable "alb_dns_name" {
  description = "ALB DNS name for API ingress"
  type        = string
  default     = "mt-apps-ingress-978b1006d8a9d559.elb.eu-central-1.amazonaws.com"
}

variable "create_api_record" {
  description = "Create api.{domain} DNS record"
  type        = bool
  default     = true
}

variable "create_root_record" {
  description = "Create {domain} (root) DNS record pointing to ALB"
  type        = bool
  default     = false
}```
----------------------------

----------------------------
# terraform/modules/acm-certificates/outputs.tf

```
# terraform/modules/acm-certificates/outputs.tf

# ============================================================================
# Regional Certificate Outputs
# ============================================================================

output "regional_certificate" {
  description = "Regional certificate complete details"
  value = var.create_regional_cert ? {
    id                        = aws_acm_certificate.regional[0].id
    arn                       = aws_acm_certificate.regional[0].arn
    domain_name               = aws_acm_certificate.regional[0].domain_name
    status                    = aws_acm_certificate.regional[0].status
    domain_validation_options = aws_acm_certificate.regional[0].domain_validation_options
  } : null
}

output "regional_certificate_arn" {
  description = "Regional certificate ARN (for quick reference)"
  value       = var.create_regional_cert ? aws_acm_certificate.regional[0].arn : null
}

# ============================================================================
# CloudFront Certificate Outputs
# ============================================================================

output "cloudfront_certificate" {
  description = "CloudFront certificate complete details"
  value = var.create_cloudfront_cert ? {
    id                        = aws_acm_certificate.cloudfront[0].id
    arn                       = aws_acm_certificate.cloudfront[0].arn
    domain_name               = aws_acm_certificate.cloudfront[0].domain_name
    status                    = aws_acm_certificate.cloudfront[0].status
    domain_validation_options = aws_acm_certificate.cloudfront[0].domain_validation_options
  } : null
}

output "cloudfront_certificate_arn" {
  description = "CloudFront certificate ARN (for quick reference)"
  value       = var.create_cloudfront_cert ? aws_acm_certificate.cloudfront[0].arn : null
}```
----------------------------

----------------------------
# terraform/modules/acm-certificates/main.tf

```
# terraform/modules/acm-certificates/main.tf

# ============================================================================
# Regional ACM Certificate (eu-central-1)
# For: ALB, ELB, API Gateway
# ============================================================================

resource "aws_acm_certificate" "regional" {
  count = var.create_regional_cert ? 1 : 0

  domain_name               = var.domain_name
  subject_alternative_names = var.subject_alternative_names
  validation_method         = "DNS"

  lifecycle {
    create_before_destroy = true
  }

  tags = merge(
    var.tags,
    {
      Name             = "${var.domain_name}-regional"
      CertificateType  = "Regional"
      Region           = "eu-central-1"
      ValidationMethod = "DNS"
    }
  )
}

# ============================================================================
# CloudFront ACM Certificate (us-east-1)
# ============================================================================

resource "aws_acm_certificate" "cloudfront" {
  count    = var.create_cloudfront_cert ? 1 : 0
  provider = aws.east

  domain_name               = var.domain_name
  subject_alternative_names = var.subject_alternative_names
  validation_method         = "DNS"

  lifecycle { create_before_destroy = true }

  tags = merge(
    var.tags,
    {
      Name             = "${var.domain_name}-cloudfront"
      CertificateType  = "CloudFront"
      Region           = "us-east-1"
      ValidationMethod = "DNS"
    }
  )
}
```
----------------------------

----------------------------
# terraform/modules/acm-certificates/versions.tf

```
# terraform/modules/acm-certificates/versions.tf

terraform {
  required_version = ">= 1.5.0"

  required_providers {
    aws = {
      source                = "hashicorp/aws"
      version               = "~> 6.0"
      configuration_aliases = [aws.east]
    }
  }
}```
----------------------------

----------------------------
# terraform/modules/acm-certificates/variables.tf

```
# terraform/modules/acm-certificates/variables.tf

variable "domain_name" {
  description = "Primary domain name for ACM certificate (e.g., 'afftech.xyz')"
  type        = string

  validation {
    condition     = can(regex("^[a-z0-9][a-z0-9-]*[a-z0-9]\\.[a-z]{2,}$", var.domain_name))
    error_message = "Domain name must be valid format (e.g., 'example.com')."
  }
}

variable "subject_alternative_names" {
  description = "Subject Alternative Names (e.g., ['*.example.com'])"
  type        = list(string)
  default     = []
}

variable "create_regional_cert" {
  description = "Create regional certificate (eu-central-1)"
  type        = bool
  default     = true
}

variable "create_cloudfront_cert" {
  description = "Create CloudFront certificate (us-east-1)"
  type        = bool
  default     = true
}

variable "tags" {
  description = "Additional tags for certificates"
  type        = map(string)
  default     = {}
}```
----------------------------

----------------------------
# terraform/modules/acm-dns-validation/outputs.tf

```
# terraform/modules/acm-dns-validation/outputs.tf

# ============================================================================
# Validation Records Outputs
# ============================================================================

output "validation_records" {
  description = "DNS validation records created in Cloudflare"
  value = {
    for key, record in cloudflare_dns_record.validation : key => {
      name    = record.name
      content = record.content
      type    = record.type
      domain  = local.all_validation_records[key].domain
      source  = local.all_validation_records[key].source
    }
  }
}

output "validation_records_count" {
  description = "Number of unique DNS validation records created"
  value       = length(cloudflare_dns_record.validation)
}

# ============================================================================
# Validated Certificate ARNs
# ============================================================================

output "regional_certificate_arn" {
  description = "Validated regional certificate ARN (ready to use)"
  value       = var.regional_certificate != null ? aws_acm_certificate_validation.regional[0].certificate_arn : null
}

output "cloudfront_certificate_arn" {
  description = "Validated CloudFront certificate ARN (ready to use)"
  value       = var.cloudfront_certificate != null ? aws_acm_certificate_validation.cloudfront[0].certificate_arn : null
}

# ============================================================================
# Debug Information
# ============================================================================

output "deduplication_info" {
  description = "Shows how many records were deduplicated"
  value = {
    regional_records_count   = length(local.regional_records)
    cloudfront_records_count = length(local.cloudfront_records)
    total_unique_records     = length(local.all_validation_records)
    duplicates_removed       = (length(local.regional_records) + length(local.cloudfront_records)) - length(local.all_validation_records)
  }
}```
----------------------------

----------------------------
# terraform/modules/acm-dns-validation/main.tf

```
# terraform/modules/acm-dns-validation/main.tf

# ============================================================================
# Locals: Extract and Deduplicate Validation Records
# ============================================================================

locals {
  # Extract regional validation records
  regional_groups = var.regional_certificate != null ? {
    for dvo in var.regional_certificate.domain_validation_options :
    trimsuffix(dvo.resource_record_name, ".") => {
      name    = trimsuffix(dvo.resource_record_name, ".")
      content = trimsuffix(dvo.resource_record_value, ".")
      type    = dvo.resource_record_type
      domain  = dvo.domain_name
      source  = "regional"
    }...
  } : {}

  regional_records = {
    for k, v in local.regional_groups :
    k => v[length(v) - 1]
  }

  cloudfront_groups = var.cloudfront_certificate != null ? {
    for dvo in var.cloudfront_certificate.domain_validation_options :
    trimsuffix(dvo.resource_record_name, ".") => {
      name    = trimsuffix(dvo.resource_record_name, ".")
      content = trimsuffix(dvo.resource_record_value, ".")
      type    = dvo.resource_record_type
      domain  = dvo.domain_name
      source  = "cloudfront"
    }...
  } : {}

  cloudfront_records = {
    for k, v in local.cloudfront_groups :
    k => v[length(v) - 1]
  }

  all_validation_records = merge(
    local.regional_records,
    local.cloudfront_records
  )
}

# ============================================================================
# Cloudflare DNS Validation Records
# Creates deduplicated CNAME records for ACM validation
# ============================================================================

resource "cloudflare_dns_record" "validation" {
  for_each = local.all_validation_records

  zone_id = var.cloudflare_zone_id
  name    = each.value.name
  content = each.value.content
  type    = each.value.type
  ttl     = 1
  proxied = false

  comment = "ACM validation - ${each.value.domain} (${each.value.source})"

  lifecycle {
    create_before_destroy = true
  }
}

# ============================================================================
# Certificate Validation Wait
# Waits for AWS to validate certificates via DNS
# ============================================================================

resource "aws_acm_certificate_validation" "regional" {
  count = var.regional_certificate != null ? 1 : 0

  certificate_arn = var.regional_certificate.arn
  validation_record_fqdns = [
    for rec in cloudflare_dns_record.validation : rec.name
  ]

  timeouts {
    create = var.validation_timeout
  }

  depends_on = [cloudflare_dns_record.validation]
}

resource "aws_acm_certificate_validation" "cloudfront" {
  count    = var.cloudfront_certificate != null ? 1 : 0
  provider = aws.east

  certificate_arn = var.cloudfront_certificate.arn

  validation_record_fqdns = [
    for rec in cloudflare_dns_record.validation : rec.name
  ]

  timeouts {
    create = var.validation_timeout
  }

  depends_on = [cloudflare_dns_record.validation]
}
```
----------------------------

----------------------------
# terraform/modules/acm-dns-validation/versions.tf

```
# terraform/modules/acm-dns-validation/versions.tf

terraform {
  required_version = ">= 1.5.0"

  required_providers {
    aws = {
      source                = "hashicorp/aws"
      version               = "~> 6.0"
      configuration_aliases = [aws.east]
    }
    cloudflare = {
      source  = "cloudflare/cloudflare"
      version = "~> 5.0"
    }
  }
}```
----------------------------

----------------------------
# terraform/modules/acm-dns-validation/variables.tf

```
# terraform/modules/acm-dns-validation/variables.tf

variable "domain_name" {
  description = "Domain name being validated"
  type        = string
}

variable "cloudflare_zone_id" {
  description = "Cloudflare Zone ID where DNS records will be created"
  type        = string

  validation {
    condition     = can(regex("^[a-f0-9]{32}$", var.cloudflare_zone_id))
    error_message = "Cloudflare Zone ID must be 32 hexadecimal characters."
  }
}

variable "regional_certificate" {
  description = "Regional certificate object from acm-certificates module"
  type = object({
    id                        = string
    arn                       = string
    domain_name               = string
    status                    = string
    domain_validation_options = set(object({
      domain_name           = string
      resource_record_name  = string
      resource_record_type  = string
      resource_record_value = string
    }))
  })
  default = null
}

variable "cloudfront_certificate" {
  description = "CloudFront certificate object from acm-certificates module"
  type = object({
    id                        = string
    arn                       = string
    domain_name               = string
    status                    = string
    domain_validation_options = set(object({
      domain_name           = string
      resource_record_name  = string
      resource_record_type  = string
      resource_record_value = string
    }))
  })
  default = null
}

variable "validation_timeout" {
  description = "Maximum time to wait for certificate validation"
  type        = string
  default     = "45m"

  validation {
    condition     = can(regex("^[0-9]+(m|h)$", var.validation_timeout))
    error_message = "Timeout must be in format like '45m' or '1h'."
  }
}```
----------------------------

